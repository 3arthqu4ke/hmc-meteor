plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
    id 'com.gradleup.shadow' version '8.3.0'
}

base {
    archivesBaseName = project.archives_base_name
    version = project.mod_version
    group = project.maven_group
}

repositories {
    maven {
        name = "Meteor Dev Releases"
        url = "https://maven.meteordev.org/releases"
    }
    maven {
        name = "Meteor Dev Snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
    maven {
        name '3arthMaven'
        url 'https://3arthqu4ke.github.io/maven'
    }
}

runClient {
    standardInput = System.in
}

dependencies {
    implementation 'me.earth.headlessmc:headlessmc-runtime:2.0.0'
    implementation ('me.earth.headlessmc:headlessmc-launcher:2.0.0') {
        transitive false
    }
    implementation ('me.earth.headlessmc:hmc-specifics-api:2.1.0') {
        transitive false
    }

    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Meteor
    modImplementation "meteordevelopment:meteor-client:${project.meteor_version}"

    include "me.earth.headlessmc:hmc-specifics-1.21.1:2.1.0:fabric-release"
}

tasks {
    processResources {
        def propertyMap = [
            "version"   : project.version,
            "mc_version": project.minecraft_version,
        ]

        filesMatching("fabric.mod.json") {
            expand(propertyMap)
        }
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.base.archivesBaseName}" }
        }
    }

    // we compile for java 8, because the headlessmc launcher might run with java 8
    // and for that we want our launcher plugin to load
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 8
    }
}
